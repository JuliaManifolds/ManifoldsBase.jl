"""
    AbstractNumbers

An abstract type to represent the number system on which a manifold is built.

This provides concrete number types for dispatch. The two most common number types are
the fields [`RealNumbers`](@ref) (`‚Ñù` for short) and [`ComplexNumbers`](@ref) (`‚ÑÇ`).
"""
abstract type AbstractNumbers end

"""
    RealNumbers <: AbstractNumbers
    ‚Ñù = RealNumbers()

The field of real numbers.
"""
struct RealNumbers <: AbstractNumbers end

"""
    ComplexNumbers <: AbstractNumbers
    ‚ÑÇ = ComplexNumbers()

The field of complex numbers.
"""
struct ComplexNumbers <: AbstractNumbers end

"""
    QuaternionNumbers <: AbstractNumbers
    ‚Ñç = QuaternionNumbers()

The division algebra of quaternions.
"""
struct QuaternionNumbers <: AbstractNumbers end

const ‚Ñù = RealNumbers()
const ‚ÑÇ = ComplexNumbers()
const ‚Ñç = QuaternionNumbers()

"""
    _unify_number_systems(ùîΩs::AbstractNumbers...)

Compute a number system that includes all given number systems (as sub-systems) and is
closed under addition and multiplication.
"""
function _unify_number_systems(a::AbstractNumbers, rest::AbstractNumbers...)
    return _unify_number_systems(a, _unify_number_systems(rest...))
end
_unify_number_systems(ùîΩ::AbstractNumbers) = ùîΩ
_unify_number_systems(r::RealNumbers, ::RealNumbers) = r
_unify_number_systems(::RealNumbers, c::ComplexNumbers) = c
_unify_number_systems(::RealNumbers, q::QuaternionNumbers) = q
_unify_number_systems(c::ComplexNumbers, ::RealNumbers) = c
_unify_number_systems(c::ComplexNumbers, ::ComplexNumbers) = c
_unify_number_systems(::ComplexNumbers, q::QuaternionNumbers) = q
_unify_number_systems(q::QuaternionNumbers, ::RealNumbers) = q
_unify_number_systems(q::QuaternionNumbers, ::ComplexNumbers) = q
_unify_number_systems(q::QuaternionNumbers, ::QuaternionNumbers) = q

Base.show(io::IO, ::RealNumbers) = print(io, "‚Ñù")
Base.show(io::IO, ::ComplexNumbers) = print(io, "‚ÑÇ")
Base.show(io::IO, ::QuaternionNumbers) = print(io, "‚Ñç")

@doc raw"""
    real_dimension(ùîΩ::AbstractNumbers)

Return the real dimension $\dim_‚Ñù ùîΩ$ of the [`AbstractNumbers`](@ref) system `ùîΩ`.
The real dimension is the dimension of a real vector space with which a number in `ùîΩ` can be
identified.
For example, [`ComplexNumbers`](@ref) have a real dimension of 2, and
[`QuaternionNumbers`](@ref) have a real dimension of 4.
"""
function real_dimension(ùîΩ::AbstractNumbers)
    return error("real_dimension not defined for number system $(ùîΩ)")
end
real_dimension(::RealNumbers) = 1
real_dimension(::ComplexNumbers) = 2
real_dimension(::QuaternionNumbers) = 4

@doc raw"""
    number_system(M::AbstractManifold{ùîΩ})

Return the number system the manifold `M` is based on, i.e. the parameter `ùîΩ`.
"""
number_system(M::AbstractManifold{ùîΩ}) where {ùîΩ} = ùîΩ
